plugins {
    id 'io.franzbecker.gradle-lombok' version '4.0.0' apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    java {
        withSourcesJar()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
            }
        }
    }

    task install(dependsOn: tasks.publishToMavenLocal) {
    }
}

subprojects {
    apply plugin: 'java-library'

    targetCompatibility = sourceCompatibility = 11

    dependencies {
        implementation platform('org.springframework.boot:spring-boot-dependencies:2.3.4.RELEASE')

        api 'com.github.bsideup.liiklus:client:0.10.0-rc1'

        testImplementation 'com.github.bsideup.liiklus:testing:0.10.0-rc1'

        testImplementation 'org.awaitility:awaitility:4.1.0'

        testImplementation platform('org.junit:junit-bom:5.7.2')

        testImplementation platform('org.testcontainers:testcontainers-bom:1.15.3')

        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'
        testImplementation 'org.testcontainers:junit-jupiter'
    }

    test {
        useJUnitPlatform()
    }
}
